/*-----------------------------------------------------------------------------------------------------------------------------------------------
Compañia: 		Eficacia S.A
Proceso:		Transformacion Punto de Venta
Desarrollador: 	Sergio Grisales Madera
Email:			sergio_grisales@eficacia.com.co
Fecha			2023/01/27 (YYYY/MM/DD)
-----------------------------------------------------------------------------------------------------------------------------------------------*/
PuntoVenta_Tmp1:
    LOAD
        PUNTO_VENTA_ID			as Id_PuntoVenta,
        CIUDAD_ID				as Id_Ciudad,
        CIUDAD					as NombreCiudad,
        DEPARTAMENTO_ID			as Id_Dpto,
        DEPTO					as NombreDepto,
        LATITUD					as Latitud_Pdv,
        LONGITUD				as Longitud_Pdv,
        OBSERVACIONES			as Observaciones,
        DIRECCION				as Direccion,
        NOMBRE					as Nombre_Pdv,
        CADENA					as Cadena,
        CANAL					as Canal,
        SUBCANAL				as SubCanal,
        TIPOLOGIA				as Tipologia,
        REGIONAL				as Regional,
        FORMATO                 as Formato,
        CIRCUITO_ID             as Distrito
    FROM 
    [lib://COMP_Stage (eficacia_admin_analitica)/SGI/Colombia/CLIIENTEUNO/IPV_PuntoVenta.qvd]
    (qvd);

/*--------------*/
Tildes:
    Mapping 
    LOAD * INLINE [
        Valor,Resultado
        Á,A
        á,a
        É,E
        é,e
        Í,I
        í,i
        Ó,O
        ó,o
        Ú,U
        ú,u];

/*--------------*/
NoConcatenate
PuntoVenta_Tmp2:
    LOAD
        Id_Dpto&Id_Ciudad&Id_PuntoVenta&Direccion 	    as CK.PuntoReferenciaUnico,
        FirstValue(Latitud_Pdv&'|'&PurgeChar(Longitud_Pdv, ' ')) 		as CK.LatLong
    Resident PuntoVenta_Tmp1
    Group By 
        Id_Dpto,
        Id_Ciudad,
        Id_PuntoVenta,
        Direccion;

Left Join
    LOAD Distinct
        Id_Dpto&Id_Ciudad&Id_PuntoVenta&Direccion			            as CK.PuntoReferenciaUnico,
        Latitud_Pdv&'|'&Longitud_Pdv						            as CK.LatLong,
        Capitalize(MapSubstring('Tildes',NombreCiudad))		            as SK.GeoCiudad, 
        Id_PuntoVenta,
        Capitalize(NombreCiudad)	                                    as NombreCiudad,
        Id_Dpto,
        NombreDepto,
        Replace(Latitud_Pdv, '.', ',')                                  as Latitud_Pdv,
        Replace(PurgeChar(Longitud_Pdv, ' '), '.', ',')                 as Longitud_Pdv,
        Observaciones,
        Direccion,
        Capitalize(Nombre_Pdv)                                          as Nombre_Pdv,
        Capitalize(Cadena)                                                   as Cadena,
        Capitalize(Canal)                                                    as Canal,
        Capitalize(SubCanal)                                            as SubCanal,
        Tipologia,
        Capitalize(Regional)                                                 as Regional,
        Capitalize(Formato)                                             as Formato

    Resident PuntoVenta_Tmp1;
    DROP Table PuntoVenta_Tmp1;

/*----------*/
NoConcatenate
PuntoVenta:
    LOAD Distinct
        Id_PuntoVenta		as SK.PuntoVenta,
        NombreCiudad		as PuntoVenta.NombreCiudad,
        NombreDepto			as PuntoVenta.NombreDepto, 
        Nombre_Pdv			as PuntoVenta.Nombre_Pdv,
        Id_PuntoVenta		as PuntoVenta.Id_PuntoVenta,
        Direccion			as PuntoVenta.Direccion, 
        Cadena				as PuntoVenta.Cadena,
        Canal				as PuntoVenta.Canal,
        SubCanal			as PuntoVenta.SubCanal,
        Tipologia			as PuntoVenta.Tipologia,
        Regional			as PuntoVenta.Regional,
        Formato             as PuntoVenta.Formato,
        Latitud_Pdv         as PuntoVenta.Latitud,
        Longitud_Pdv        as PuntoVenta.Longitud
        
    Resident PuntoVenta_Tmp2;
    DROP Table PuntoVenta_Tmp2;

STORE PuntoVenta INTO [lib://COMP_DataMart (eficacia_admin_analitica)/SGI/Colombia/CLIIENTEUNO/_PuntoVenta.qvd];
Drop Table PuntoVenta;